apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = "1.0.6"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    resourcePrefix "AndroidMP3RecorderLibrary_"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
    }

    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
        jniLibs.srcDir 'src/main/libs' // This is not necessary unless you have precompiled libraries in your project.
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
}

def siteUrl = 'https://github.com/ProFive/AndroidMP3Recorder'
def gitUrl = 'https://github.com/ProFive/AndroidMP3Recorder.git'
group = "com.czt.mp3recorder"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            groupId = 'com.github.ProFive'
            artifactId = 'AndroidMP3Recorder'
            version = '1.0.6'

            pom {
                packaging 'aar'
                name = 'Android MP3 Recorder Library'
                description = 'A library for recording MP3 audio on Android'
                url = siteUrl
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ProFive'
                        name = 'ProFive'
                        email = 'ngvannam5@gmail.com'
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}